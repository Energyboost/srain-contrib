# Maintainer: Shengyu Zhang <la@archlinuxcn.org>

_pkgname=srain-git
pkgname=${MINGW_PACKAGE_PREFIX}-${_pkgname}
_pkgver=1.0.0rc1
pkgver=1.0.0rc1.1067.0542dd1
pkgrel=3
pkgdesc="Modern IRC client, git version (mingw-w64)"
arch=('any')
license=('GPL')
url="https://srain.im"
makedepends=("git"
             "make"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-gettext")
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-libconfig"
         "${MINGW_PACKAGE_PREFIX}-libsoup")
optdepends=("${MINGW_PACKAGE_PREFIX}-glib-networking: TLS connection support")
conflicts=("${MINGW_PACKAGE_PREFIX}-srain")
provides=("${MINGW_PACKAGE_PREFIX}-srain")
source=("git+https://github.com/SilverRainZ/srain.git#branch=master")
sha256sums=('SKIP')
options=('!strip' '!buildflags' 'staticlibs')

_prefix=${MINGW_PREFIX}

pkgver() {
    cd ${_pkgname%-git}
    echo "${_pkgver}.$(git rev-list --count HEAD).$(git describe --always)"
}


build() {
    cd ${_pkgname%-git}

    ./configure                         \
        --prefix="${_prefix}"           \
        --datadir="${_prefix}/share"    \
        --sysconfdir="${_prefix}/etc"   \
        --disable-debug
    make clean
    make
}

package() {
    cd ${_pkgname%-git}

    make DESTDIR="${pkgdir}" install
    mv "${pkgdir}/${_prefix}/bin/${_pkgname%-git}"{,.exe}
}
